config {
     database: "ecf-bq",
    name: "npq_ofsted_establishments",
    type: "table",
    assertions: {uniqueKey: ["establishment_urn"]},
    description: "This mart provides the most recent version of all Ofsted establishments. This mart joins the most recent inspection extract to the all_gias_ofsted_establishments table. Every June and December new extracts should be investigated and added to this mart to ensure the list of all Ofsted estbalihsments is correct. Additional extracts should be saved as a static table added to the mart using UNION ALL and the new list filtered for all establishment urns that are not in the stored in this mart. New extracts of Ofsted establishments can be found here: https://www.gov.uk/government/statistical-data-sets/childcare-providers-and-inspections-management-information",
    columns: {
        establishment_urn: "The unique reference number of the school or establishment inspected by Ofsted.",
        phase_of_education: "Phase of education of an establishment as stored in Ofsted.",
        establishment_type: "Establishment type as stored in Ofsted.",
        establishment_subtype: "Establishment subtype as stored in Ofsted.",
        la_name: "Name of local authority.",
    }
}

-- Extracting information from the Ofsted Establishments December 2024 extract
WITH ofsted_establishments_latest AS (
  SELECT 
    provider_URN as establishment_urn,
    'Childcare provider' AS phase_of_education,
    Provider_Type AS establishment_type,
    Provider_Subtype AS establishment_subtype,
    Local_Authority AS la_name
  FROM ${ref('Ofsted_Establishments_December_2024')}
)

SELECT
    *
  FROM
    ofsted_establishments_latest
  
  UNION ALL 
  -- UNION ALL is used to select all establishments from latest_early_years_settings CTE and establishments in the...  
  -- ... all_gias_ofsted_establishments table which aren't in the latest_early_years_settings CTE. 
  -- Both tables are not a complete list of establishments (Some in one aren't contained in the other and vise versa).
  SELECT
    establishment_urn,
    phase_of_education,
    establishment_type,
    establishment_subtype,
    la_name
  FROM
     ${ref('all_gias_ofsted_establishments')}
  WHERE
    data_source LIKE 'Ofsted%'
    -- Filters all establishment urns that are not in the latest_early_years_settings 
    AND establishment_urn NOT IN (
    SELECT
      establishment_urn
    FROM
      ofsted_establishments_latest)

  /*
  -- Blocked out query which could used for the Ofsted Establishments June 2025 extract when it becomes available, investigation into counts and any closures should be made before any commits are made
  -- June and following extracts should be found here:  https://www.gov.uk/government/statistical-data-sets/childcare-providers-and-inspections-management-information
  UNION ALL
  SELECT 
    provider_URN as establishment_urn,
    'Childcare provider' AS phase_of_education,
    Provider_Type AS establishment_type,
    Provider_Subtype AS establishment_subtype,
    Local_Authority AS la_name
  FROM ${ref('Ofsted_Establishments_June_2025')} 
  WHERE establishment_urn NOT IN (
      SELECT
        establishment_urn
      FROM
        ofsted_establishments_latest
      UNION ALL
      SELECT 
        establishment_urn
      FROM 
        ${ref('all_gias_ofsted_establishments')}) 
  */




