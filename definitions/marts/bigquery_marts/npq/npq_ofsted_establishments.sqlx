config {
     database: "ecf-bq",
    name: "npq_ofsted_establishments",
    type: "table",
    assertions: {uniqueKey: ["establishment_urn"]},
    description: "This mart provides information about the most recent version of all Ofsted establishments. This mart appends the most recent inspection extract to the previous extracts of Ofsted Establishments: all_gias_ofsted_establishments. This mart uses a UNION ALL function to join all Ofsted establishments from the most recent extract to any Ofsted establishments in previous extracts which aren't in the most recent extract. This will ensure the Ofsted list cpatires all schools or establishments. New extracts of Ofsted establishments are added every June or December which can be found here: https://www.gov.uk/government/statistical-data-sets/childcare-providers-and-inspections-management-information",
    columns: {
        establishment_urn: "The unique reference number of the school or establishment inspected by Ofsted.",
        phase_of_education: "Phase of education of an establishment as stored in Ofsted.",
        establishment_type: "Establishment type as stored in Ofsted.",
        establishment_subtype: "Establishment subtype as stored in Ofsted.",
        la_name: "Name of local authority.",
    }
}

-- Extracting information from the Ofsted Establishments December 2024 extract
WITH ofsted_establishments_December_2024 AS (
  SELECT 
    provider_URN as establishment_urn,
    'Childcare provider' AS phase_of_education,
    Provider_Type AS establishment_type,
    Provider_Subtype AS establishment_subtype,
    Local_Authority AS la_name
  FROM ${ref('Ofsted_Establishments_December_2024')}
)

SELECT
    *
  FROM
    ofsted_establishments_December_2024
  
  UNION ALL 
  -- UNION ALL is used to select all establishments from ofsted_establishments_December_2024 CTE and establishments in the...  
  -- ... all_gias_ofsted_establishments table which aren't in the ofsted_establishments_December_2024 CTE. 
  -- Both tables are not a complete list of establishments (Some in one aren't contained in the other and vise versa).
  SELECT
    establishment_urn,
    phase_of_education,
    establishment_type,
    establishment_subtype,
    la_name
  FROM
     ${ref('all_gias_ofsted_establishments')}
  WHERE
    data_source LIKE 'Ofsted%'
    -- Filters all establishment urns that are not in the ofsted_establishments_December_2024 
    AND establishment_urn NOT IN (
    SELECT
      establishment_urn
    FROM
      ofsted_establishments_December_2024)

  /*
  --To adjust this query to be used with future extracts, it should
    -- the current most recent extract should be stored as a CTE similar to ofsted_establishments_December_2024
    -- All following extracts should be appended to the latest extract to ensure the most up to date version of the data is the source (Pulling only from historical extracts if the latest version doesn't contain them)
        -- The code should follow something like: 
            -- Most recent extact 
            -- UNION all 
            -- second most recent extract where establishment_urn not in most recent extract 
            -- UNION ALL
            -- all_gias_ofsted_establishments where establishment_urn not in most recent extract and second most recent extract

  SELECT
    *
  FROM
    ofsted_establishments_June_2025
  UNION ALL
  SELECT
    *
  FROM
    ofsted_establishments_December_2024
    WHERE establishment_urn NOT IN (SELECT establishment_urn FROM ofsted_establishments_June_2025)
  UNION ALL 
  SELECT 
    provider_URN as establishment_urn,
    'Childcare provider' AS phase_of_education,
    Provider_Type AS establishment_type,
    Provider_Subtype AS establishment_subtype,
    Local_Authority AS la_name
  FROM ${ref('Ofsted_Establishments_June_2025')} 
  WHERE establishment_urn NOT IN (
      SELECT establishment_urn 
      FROM ofsted_establishments_December_2024
      UNION ALL
      SELECT establishment_urn
      FROM ${ref('all_gias_ofsted_establishments')}) 
  */




