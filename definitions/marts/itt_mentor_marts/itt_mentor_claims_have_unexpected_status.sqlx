config {
  type: "assertion",
  description: "If this assertion fails then the itt_mentor_claims table contains an unexpected ITT mentor claim status from the ITT mentor service.\nTo fix, liaise with the ITT team to define the new statuses and determine whether they should be included. Then add permissable claim statuses to the SQL logic in mentors_and_providers_itt_ecf_combined.sqlx. Finally, add the new status and definition to the expected status list in this assertion."
}

SELECT
    earliest_claim_current_status,
    COUNT(*) AS number_of_claims_with_unexpected_status
  FROM
    ${ref("itt_mentor_claims")}
  WHERE
    earliest_claim_current_status NOT IN (
      "paid" /* A payment has been made to the claimant. In future the payment may be sampled for quality assurance. */,
      "sampling_in_progress" /* A payment has been made to the claimant and it is currently being quality assured as part of a sample. */,
      "sampling_provider_not_approved" /* A payment was made to the claimant but it now should be or has been clawed back */,
      "sampling_not_approved" /* Same as sampling_provider_not_approved but for a different problem with the claim */,
      "submitted", /* A claim has been created by a claimant but no payment has yet been made, nor has a decision been taken about whether to pay the claimant */
      "clawback_in_progress", /* A claim was paid but is now being clawed back from the recipient */
      "draft", /*a claim has been drafted by support but the school hasn't yet decided whether to submit it or not */
      "invalid_provider", /*a set of historic claims that will not be paid unless the claimant changes them to a valid provider*/)
  GROUP BY
    earliest_claim_current_status