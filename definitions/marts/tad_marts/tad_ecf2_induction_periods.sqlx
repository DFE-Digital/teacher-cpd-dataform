config {
    schema: "TAD",
    database: "ecf-bq",
    name: "tad_ecf2_induction_periods",
    type: "table",
    assertions: {
        uniqueKey: ["unique_id"]
    },
    bigquery: {
        clusterBy: ["appropriate_body_name", "trn"]
    },
    description: "This table is a record of all induction periods for an Early Career Teacher (ECT) reported by Appropriate Bodies with an overall induction status of in progress that are stored in the Record Inductions as an Appropriate Body service (RIAB) or the Database of Qualified Teachers (DQT). An overall induction status of in progress is defined as the ECT having at least one induction period which has no finished_on date. Data taken from the DQT is defined using the rule: The teacher is not stored RIAB and has an induction period start or finish date after 01/09/2021. ",
    columns: {
        unique_id: "Unique id is for identifying different rows in this table, generated to join two tables together. It has no significance to RIAB or static table data extracts.",
        trn: {
            description: "This comes from the teacher profile which we access via the participant profile",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        appropriate_body_name: "The name of the appropriate body.",
        started_date: "Start date for this induction period",
        finished_on: "End date for this induction period. This is not the completion date for a participant's full induction.",
        number_of_terms: "The number of terms served during the induction period.",
        induction_programme_type: "The induction programme the school offers (FIP / CIP / DIY) : potential values: full_induction_programme core_induction_programme, design_our_own, school_funded_fip ",
        data_source: "Where the induction period is held, possible values: RIAB or historic import (DQT)"
    }
}

  /*TAD require:
  All induction data stored in RIAB (currently all induction periods for ECT's with an overall induction status of in progress)
  All induction periods from DQT for each teacher that is captured in the subset...
  The teacher is not stored RIAB and has an induction period start or finish date after 01/09/2021
  */
WITH
  ecf2_induction_periods AS (
  SELECT
    teachers.trn AS trn,
    appropriate_body_name AS appropriate_body_name,
    started_on AS started_on,
    finished_on AS finished_on,
    CAST(number_of_terms AS INT64) AS number_of_terms,
    induction_programme AS induction_programme_type,
    'RIAB' AS data_source
  FROM
    ${ref(`ecf2_teacher_induction_periods`)}),
  historical_inductions AS (
  SELECT
    CAST(historic.trn AS string) AS trn,
    abs.name AS appropriate_body_name,
    historic.started_on AS started_on,
    historic.finished_on AS finished_on,
    historic.number_of_terms AS number_of_terms,
    historic.induction_programme_choice AS induction_programme_type,
    'Historic import' AS data_source
  FROM
    `static_tables.historical_induction_period_import` AS historic
  LEFT JOIN
    ${ref(`appropriate_bodies_latest_ecf2`)} AS abs
  ON
    historic.appropriate_body_id = abs.legacy_id )
SELECT
  -- unique_id does not signify anything. It is used for this mart and not associated with RIAB or DQT inductiond period data.
  ROW_NUMBER() OVER () AS unique_id,
  *
FROM
  ecf2_induction_periods
UNION ALL
SELECT
  ROW_NUMBER() OVER () + (
  SELECT
    COUNT(*)
  FROM
    ecf2_induction_periods) AS unique_id,
  -- Ensures continuity of unique id
  *
FROM
  historical_inductions
WHERE
  trn IN (
  SELECT
    DISTINCT trn
  FROM
    historical_inductions
  WHERE
    (started_on >= '2021-09-01'
      OR finished_on >= '2021-09-01')
    AND trn NOT IN (
    SELECT
      DISTINCT trn
    FROM
      ecf2_induction_periods) )
