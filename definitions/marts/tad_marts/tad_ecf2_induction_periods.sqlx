config {
    schema: "TAD",
    database: "ecf-bq",
    name: "ecf2_induction_periods",
    type: "table",
    assertions: {
        uniqueKey: ["unique_id"]
    },
    bigquery: {
        clusterBy: ["appropriate_body_name", "trn"]
    },
    description: "This table is a record of all induction periods for an Early Career Teacher (ECT) where a proportion of their induction has been served since 01/09/2021. All induction periods in this data have been reported by Appropriate Bodies. This table combines data stored in the Record Inductions as an Appropriate Body (RIAB) service and the Database of Qualified Teachers (DQT) with the records origin detailed by the data_source field, it's possible values are either 'RIAB' or 'Historic import'. This data contains all ECTs with an at least one in progress or completed induction period on or after 01/09/2021. An in progress induction period is defined by an Appropriate Body having reported a start date and no finish date for a record. This table has been designed specially for TAD's analysis of ECTs induction periods reported by Appropriate Bodies.",
    columns: {
        unique_id: "Unique id is for identifying different rows in this table, generated to join two tables together. It has no significance to RIAB or static table data extracts.",
        trn: {
            description: "This comes from the teacher profile which we access via the participant profile. It has been validated with the teacher's date of birth.",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        appropriate_body_name: "The name of the Appropriate Body that recorded the induction period served by the ECT.",
        started_date: "The date reported by the Appropriate Body when the ECT started the induction period. This field should not be used to indicate the ECT's start date for their full induction.",
        finished_on: "The date reported by the Appropriate Body when the ECT finished the induction period. This field should not be used to indicate the ECT's end date for their full induction.",
        number_of_terms: "The number of terms the induction period counts towards induction, as reported by the Appropriate Body. Values for induction records from RIAB will contain decimal places whereas historical records will not contain decimal places.",
        induction_programme_type: "The type of training that a school supported the ECT's induction with for that induction period, as reportedd by the Appropriate Body. Potential values: full_induction_programme core_induction_programme, design_our_own, school_funded_fip ",
        data_source: "Where the induction period is currently held, possible values: 'RIAB' or 'historic_import' (DQT)."
    }
}

WITH
  ecf2_induction_periods AS (
  SELECT
    trn AS trn,
    appropriate_body_name AS appropriate_body_name,
    started_on AS started_on,
    finished_on AS finished_on,
    number_of_terms AS number_of_terms,
    induction_programme AS induction_programme_type,
    'RIAB' AS data_source
  FROM
    ${ref(`ecf2_teacher_induction_periods`)}),
  historical_inductions AS (
  SELECT
    CAST(historic.trn AS string) AS trn,
    abs.name AS appropriate_body_name,
    historic.started_on AS started_on,
    historic.finished_on AS finished_on,
    historic.number_of_terms AS number_of_terms,
    historic.induction_programme_choice AS induction_programme_type,
    'historic_import' AS data_source
  FROM
    `static_tables.historical_induction_period_import` AS historic
  LEFT JOIN
    ${ref(`appropriate_bodies_latest_ecf2`)} AS abs
  ON
    historic.appropriate_body_id = abs.legacy_id )
SELECT
  -- unique_id does not signify anything. It is used for this mart and not associated with RIAB or DQT inductiond period data.
  ROW_NUMBER() OVER () AS unique_id,
  *
FROM
  ecf2_induction_periods
WHERE
  -- Selecting all induction records from RIAB for an ECT which either started or finished at least one induction period on or after 01/09/2021.
  trn IN (
SELECT
  DISTINCT trn
FROM
  ecf2_induction_periods
WHERE
  (started_on >= '2021-09-01'
    OR finished_on >= '2021-09-01') )
UNION ALL
SELECT
  ROW_NUMBER() OVER () + (
  SELECT
    COUNT(*)
  FROM
    ecf2_induction_periods) AS unique_id,
  -- Ensures continuity of unique id
  *
FROM
  historical_inductions
WHERE
  -- Selecting all induction records from the DQT for an ECT which is not already captured in the RIAB extract and either started or finished at least one induction period on or after 01/09/2021
  trn IN (
SELECT
  DISTINCT trn
FROM
  historical_inductions
WHERE
  (started_on >= '2021-09-01'
    OR finished_on >= '2021-09-01')
  AND trn NOT IN (
  SELECT
    DISTINCT trn
  FROM
    ecf2_induction_periods
  WHERE
    (started_on >= '2021-09-01'
      OR finished_on >= '2021-09-01')) )
