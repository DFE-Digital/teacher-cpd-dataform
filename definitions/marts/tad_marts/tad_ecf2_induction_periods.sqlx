config {
    schema: "TAD",
    database: "ecf-bq",
    name: "tad_ecf2_induction_periods",
    type: "table",
    assertions: {
        uniqueKey: ["unique_id"]
    },
    bigquery: {
        clusterBy: ["appropriate_body_name", "trn"]
    },
    description: "This table is a record of: All induction data stored in RIAB (currently all induction periods for ECT's with an overall induction status of in progress. As well as all induction periods from DQT for each teacher that is captured in the subset: The teacher is not stored RIAB and has an induction period start or finish date after 01/09/2021. This table is intended for TAD's analytics where a single row is a single induction period.",
    columns: {
        unique_id: "Unique id is for indentifying different rows in this table, it has no significance to RIAB or static table data extracts.",
        trn: {
            description: "This comes from a participant profile",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        appropriate_body_id: "A uniqune organisation identifier used for identifying appropriate bodies",
        legacy_appropriate_body_id: "A legacy unique organisation identifier taken from DfE Sign-In used for identifying appropriate bodies.",
        appropriate_body_name: "The name of the appropriate body, each name has an ID and legacy ID.",
        started_date: "Start date for this induction period",
        finished_on: "End date for this induction period. This is not the completion date for a participant's full induction.",
        number_of_terms: "The number of terms served during the induction period.",
        induction_programme_type: "The induction programme the school offers (FIP / CIP / DIY) : potential values: full_induction_programme core_induction_programme, design_our_own, school_funded_fip ",
        data_source: "Where the induction period is recorded, possible values: RIAB or historic import (DQT)"
    }
}

  /*TAD require:
  All induction data stored in RIAB (currently all induction periods for ECT's with an overall induction status of in progress)
  All induction periods from DQT for each teacher that is captured in the subset...
  The teacher is not stored RIAB and has an induction period start or finish date after 01/09/2021
  */
WITH
  ecf2_induction_periods AS (
  SELECT
    teachers.trn AS trn,
    induction_periods.appropriate_body_id AS appropriate_body_id,
    'N/A' AS legacy_appropriate_body_id,
    abs.name AS appropriate_body_name,
    induction_periods.started_on AS started_on,
    induction_periods.finished_on AS finished_on,
    CAST(induction_periods.number_of_terms AS INT64) AS number_of_terms,
    induction_periods.induction_programme AS induction_programme_type,
    'RIAB' AS data_source
  FROM
    ${ref(`induction_periods_latest_ecf2`)} AS induction_periods
  LEFT JOIN
    ${ref(`teachers_latest_ecf2`)} AS teachers
  ON
    teachers.id = induction_periods.teacher_id
  LEFT JOIN
    ${ref(`appropriate_bodies_latest_ecf2`)} AS abs
  ON
    induction_periods.appropriate_body_id = abs.id ),
  historical_inductions AS (
  SELECT
    CAST(historic.trn AS string) AS trn,
    'N/A' AS appropriate_body_id,
    historic.appropriate_body_id AS legacy_appropriate_body_id,
    abs.name AS appropriate_body_name,
    historic.started_on AS started_on,
    historic.finished_on AS finished_on,
    historic.number_of_terms AS number_of_terms,
    historic.induction_programme_choice AS induction_programme_type,
    'Historic import' AS data_source
  FROM
    `static_tables.historical_induction_period_import` AS historic
  LEFT JOIN
    ${ref(`appropriate_bodies_latest_ecf2`)} AS abs
  ON
    historic.appropriate_body_id = abs.legacy_id )
SELECT
  -- unique_id does not signify anything. It is used for this table and not associated with RIAB or DQT inductiond data.
  ROW_NUMBER() OVER () AS unique_id,
  *
FROM
  ecf2_induction_periods
UNION ALL
SELECT
  ROW_NUMBER() OVER () + (
  SELECT
    COUNT(*)
  FROM
    ecf2_induction_periods) AS unique_id,
  -- Ensures continuity of unique id
  *
FROM
  historical_inductions
WHERE
  trn IN (
  SELECT
    DISTINCT trn
  FROM
    historical_inductions
  WHERE
    (started_on >= '2021-09-01'
      OR finished_on >= '2021-09-01')
    AND trn NOT IN (
    SELECT
      DISTINCT trn
    FROM
      ecf2_induction_periods) )
