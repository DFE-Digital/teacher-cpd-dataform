config {
    schema: "TAD",
    database: "ecf-bq",
    name: "tad_npq_enrolment_v2",
    type: "table",
    assertions: {
        uniqueKey: ["application_id"]
    },
    bigquery: {
        partitionBy: "DATE(application_created_at)",
        clusterBy: ["cohort", "provider_name", "course_name", "application_status"]
    },
    description: "This table is intended for use by TAD to support their publications on NPQs. The table presents information on NPQ enrolments using the latest available data-model post separation of ECF & NPQ. It is structured around NPQ applications, with each application constituting a single record. Participant details are integrated directly into each application record.",
    columns: {
        application_id: "This is the unique id for an NPQ applications. This field was entirely generated post-separation in the new NPQ data model. We join this field on the application_id field in the declarations table to identify declarations paired with this application. This field will not map onto historical application_ids generated prior to NPQ Separation (27/11/2024). If you would like to map historical application_ids please use the application_ecf_id field that hosts historical application_ids",
        application_ecf_id: "This field contains the historical application_id that will have been created in the old data model pre-separation. This field is available in the new model but not used to join on other tables.  You can use this field if you want to map historical data that pre-dates the ECF & NPQ separation (27/12/2024).",
        teacher_profile_trn: {
            description: "This TRN is sourced through user profiles (previously Teacher Profiles). If the application has been made but the TRN has not been fully verified the TRN field will be blank despite a valid TRN auto_verified field.",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        participant_user_id: "This is the user_id that matches the user_ids created pre-separation in the ECF Teacher Profiles. This field can be used to join on ECF Teacher Profiles should they have an ECF profile or were present in NPQ data prior to 28/11/2024. This user_id is sourced from the NPQ User Profile.",
        application_trn: {
            description: "This is the TRN provided by applicants at the point of application, if a teacher_profile_trn is available that is the preferred source for a TRN as this field could have been incorrectly completed at the time of the application.  The teacher_profile_trn field contains the verified approved TRN for an individual once the appropriate checks have occurred.",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        trn_auto_verified: "This refers to whether the TRN provided in the application process was automatically verified in the application process",
        full_name: {
            description: "The full name of the applicant as stored on the NPQ service, taken from the users table.",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        preferred_name: {
            description: "The preferred name of the applicant as stored on the NPQ service, taken from the users table. If this field is null, no preferred name has been recorded by the applicant and the full_name should be used.",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        email: {
            description: "The email address of the applicant as stored on the NPQ service, taken from the users table. This email can be used to contact the applicant.",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        headteacher_status: "Indicates whether the participant is a headteacher in their first five years.",
        eligible_for_funding: "Indicates whether the applicant is eligible for funding for their course.",
        funding_choice: "If a participant is not eligible for funding, this indicates the source of the funding for their NPQ. Possible fields: Another, School, Self, Trust ",
        funded_place: "This indicates if the accepted NPQ application is being funded by the DfE as one of the capped funded places. Being eligible for funding does not guarantee the application will be funded.",
        school_urn: "The unique reference number of the participant's school. Private childcare participants do not need a URN.",
        la_name: "Name of local authority",
        course_name: "Text name of NPQ course",
        provider_name: "Name of lead provider",
        targeted_delivery_funding_eligibility: "Indicates whether this NPQ participant is eligible for targeted delivery uplift payment",
        work_setting: "The setting the participant works in as recorded on the application, this is user entered data rather than data derived from GIAS or Ofsted.",
        kind_of_nursery: "If the participant works in a nursery, indicates which type of nursery. Possible fields: Local authority maintained nursery, Preschool class as part of school, Private nursery, Another Early Years Setting",
        private_childcare_provider_urn: "The unique reference number given to the employer of participants who work in private childcare.",
        funding_eligiblity_status_code: "Indicates the funding status of a participant. ",
        cohort: "The cohort/academic year corresponding to when the participant started their course. Possible fields: 2021, 2022, 2023, 2024",
        cohort_number: "A policy-oriented identifier indicating which DfE-funded applicant cohort the record belongs to. The cohort is determined by the applicant’s cohort.start_year and, for start years 2021-2023, by the schedule_identifier. Cohort 1 is the autumn schedule in the 2021 cohort; Cohort 2, the spring schedule in the 2021 cohort; Cohort 3, the autumn schedule in the 2022 cohort; Cohort 4, the spring schedule in the 2022 cohort; Cohort 5, the autumn schedule in the 2023 cohort; and Cohort 6, the spring schedule in the 2023 cohort. All 2024 applicants are assigned to Cohort 7. For 2025, applicants are split into Cohort 8 if their application was created before 18 May 2025 (spring window) and Cohort 9 if created on or after 8 September 2025 (autumn window). Applicants not matching these rules are assigned n/a.",
        application_status: "Indicates whether the application has been approved by the lead provider.Possible fields: Pending, Accepted, Rejected ",
        updated_at: "Refers to the latest updated date for the application record",
        employer_name: "Shows the entered employer name of non-school users. Text field.",
        employment_role: "Shows the employment role of non-school users. Text field.",
        employment_type: "For non-school users, shows the type of institution they are employed in. Possible fields: Local authority supply teachers, Local authority virtual school, Young offender institute, Hospital school, Other",
        training_status: "The training status of a given participant, updated on their application record",
        schedule_identifier: "This indicates which sub-cohort or tranche the participant commenced training within an annual cohort. For NPQ, the schedule identifier also distinguishes between the leadership and specialist NPQ types. Pulled from participant profile which is created when application is accepted by lead provider",
        lead_provider_id: "Pulled from the NPQ application record. Please note values in this field will not match the same numerical value available in your historical snapshots but the lead provider has not changed.",
        primary_establishment: "Records whether the user's establishment at time of registration was a primary establishment or not. Based on GIAS data.",
        teacher_catchment: "The catchment zone the participant works in within the UK i.e. England, Wales, Northern Ireland etc. International applicants recorded as 'another'",
        teacher_catchment_country: "The country the participant works in ",
        tsf_primary_eligibility: "TRUE/FALSE for whether a school is eligible for TSF (£200) owing to it being a primary establishment.",
        tsf_primary_plus_eligibility: "TRUE/ FALSE for whether a school is eligible for the base £200 as well as the additional £600 based on whether they are a primary school and have fewer than 150 pupils. Pupil count based on GIAS data at time of registration.",
        works_in_school: "Indicates whether the participant works in a school.",
        senco_in_role: "Answer selected for question 'Are you a SENCO?' during the application stage. Possible results are; yes, no but I plan to be, no and I do not plan to be.",
        senco_start_date: "Provided at time of application and only answered if 'yes' is selected for Are you a SENCO. Format is MM/YYYY stored as a string."
    }
}

SELECT
  enrolments.application_id,
  enrolments.application_ecf_id,
  enrolments.trn_verified AS teacher_profile_trn,
  enrolments.ecf_user_id AS participant_user_id,
  enrolments.application_trn,
  enrolments.trn_auto_verified,
  users.full_name,
  users.preferred_name,
  users.email,
  enrolments.application_created_at,
  enrolments.headteacher_status,
  enrolments.eligible_for_funding,
  enrolments.funding_choice,
  enrolments.funded_place,
  enrolments.school_urn,
  enrolments.school_name,
  enrolments.la_name,
  enrolments.school_postcode,
  enrolments.course_name,
  enrolments.provider_name,
  enrolments.targeted_delivery_funding_eligibility,
  enrolments.work_setting,
  enrolments.kind_of_nursery,
  enrolments.private_childcare_provider_urn,
  enrolments.funding_eligiblity_status_code,
  enrolments.cohort,
  enrolments.cohort_number,
  enrolments.application_status,
  enrolments.updated_at,
  enrolments.employer_name,
  enrolments.employment_role,
  enrolments.employment_type,
  enrolments.training_status,
  enrolments.schedule_identifier,
  enrolments.cohort_id,
  enrolments.lead_provider_id,
  enrolments.primary_establishment,
  enrolments.teacher_catchment,
  enrolments.teacher_catchment_country,
  enrolments.teacher_catchment_iso_country_code,
  enrolments.tsf_primary_eligibility,
  enrolments.tsf_primary_plus_eligibility,
  enrolments.works_in_school,
  enrolments.senco_in_role,
  enrolments.senco_start_date
FROM
  ${ref(`npq_enrolments`)} as enrolments
LEFT JOIN ${ref('users_latest_npq')} as users
ON enrolments.user_id = users.id
