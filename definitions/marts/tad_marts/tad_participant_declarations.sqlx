config {
    type: "table",
    assertions: {
        uniqueKey: ["declaration_id"]
    },
    bigquery: {
        partitionBy: "DATE(created_at)",
        clusterBy: ["type","course_identifier","lead_provider_name","state"]
    },
    description: "",
    columns: {
        id: "UID",
        created_at: "Date this entity was created, according to the latest version of the data received from the database.",
        updated_at: "Date this entity was last updated something in the database, according to the latest version of the data received from the database.",
        course_identifier: "Use this field to join to NPQ enrolments (declaration_course_identifier)",
        delivery_partner_name: "This field is only populated for ECTs",
        declaration_date: "Date evidence was received.",
        participant_profile_id: "ID used to link to NPQ enrolment records (on application_id) and ECF induction records (participant_profile_id)",
        last_streamed_event_occurred_at: "Timestamp of the event that we think provided us with the latest version of this entity.",
        last_streamed_event_type: "Event type of the event that we think provided us with the latest version of this entity. Either entity_created, entity_updated, entity_destroyed or entity_imported."

    }
}

  /* Table Logic: This table contains all declaration records for both NPQ and ECF participants*/
SELECT
  id as declaration_id,
  created_at,
  updated_at,
  course_identifier,
  cpd_lead_provider_id,
  cpd_lead_provider_name AS lead_provider_name,
  delivery_partner_id,
  delivery_partner_name,
  declaration_date,
  declaration_type,
  participant_profile_id,
  state,
  type,
  last_streamed_event_occurred_at,
  last_streamed_event_type
FROM
  ${ref(`participant_declarations_latest_cpd`)} AS dec_cpd
LEFT JOIN
  ${ref(`cpd_lead_providers_latest_cpd`)} AS cpd_lead_providers
ON
  dec_cpd.cpd_lead_provider_id=cpd_lead_providers.id
LEFT JOIN
  ${ref(`delivery_partners_latest_cpd`)} AS delivery_partners
ON
  dec_cpd.delivery_partner_id=delivery_partners.id
