config {
    name: "ls_ecf2_post_2021_full_teacher_induction_periods_w_ppid",
    type: "table",
    bigquery: {
        clusterBy: ["appropriate_body_name", "trn", "started_on", "finished_on"]
    },
description: "USE CAREFULLY â€“ This mart has been built to cross-reference ECF2 induction periods with the ECF1 participant profile to spot inconsistencies, identify bad data, and extract records needing to be rectified, this mart has not been built for analytical purposes. This is a version of the ECF2 induction periods taken from ecf2_post_2021_full_teacher_induction_periods and the ECF1 participant profile ID joined to induction periods for an Early Career Teacher (ECT) where a proportion of their induction has been served since 01/09/2021. The participant profile ID has been included, for ECTs with multiple participant profile IDs for the same TRN, the earliest created participant_profile_id will be used. It has been built to support conversations between the CPD Data and Insight team and the RIAB or other internal teams.It combines data from the Record Inductions as an Appropriate Body (RIAB) service and the Database of Qualified Teachers (DQT), with the origin of each record shown in the data_source field, which can be either 'RIAB' or 'Historic import'. All periods have been reported by Appropriate Bodies. The dataset contains all ECTs with at least one in-progress or completed induction period on or after 01/09/2021, where an in-progress period is defined as having a reported start date but no finish date.",        unique_id: "Unique id is for identifying different rows in this table, generated to join two tables together. It has no significance to RIAB or static table data extracts.",
        trn: {
            description: "This comes from the teacher profile which we access via the participant profile. It has been validated with the teacher's date of birth.",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        appropriate_body_name: "The name of the Appropriate Body that recorded the induction period served by the ECT.",
        started_on: "The date reported by the Appropriate Body when the ECT started the induction period. This field should not be used to indicate the ECT's start date for their full induction.",
        finished_on: "The date reported by the Appropriate Body when the ECT finished the induction period. This field should not be used to indicate the ECT's end date for their full induction.",
        number_of_terms: "The number of terms the induction period counts towards induction, as reported by the Appropriate Body. Values for induction records from RIAB will contain decimal places whereas historical records will not contain decimal places.",
        induction_programme_type: "The type of training that a school supported the ECT's induction with for that induction period, as reportedd by the Appropriate Body. Potential values: full_induction_programme core_induction_programme, design_our_own, school_funded_fip ",
        data_source: "Where the induction period is currently held, possible values: 'RIAB' or 'historic_import' (DQT).",
        participant_profile_id: "The ECTs ECF 1 participant profile ID. Joined to the induction period by joining the trn contained in the teachers profile to the participants profile via teacher_profile_id, then joining that extract to the induction period via trn."
    }
}

  -- As there is a many to one mapping of teachers.id to trn, we must creating a participant profiles CTE to allow teacher_profiles to be joined to participant_profiles and filtered for ECT participant types, then joining to the induciton period via trn stored in the teacher profile. 
  -- Duplicates are removed by selecting the most earlist created participant profile for TRNs that have multiple participant profiles associated to them.
WITH participant_profiles as (
  SELECT participants.id as participant_profile_id , teachers.trn
  FROM ${ref('participant_profiles_latest_ecf1')} as participants
  LEFT JOIN ${ref('teacher_profiles_latest_ecf1')} as teachers
  ON participants.teacher_profile_id = teachers.id
  WHERE participants.type = 'ParticipantProfile::ECT'
  QUALIFY ROW_NUMBER() OVER (
  PARTITION BY teachers.trn
  ORDER BY participants.created_at ASC, participants.id
) = 1
)


SELECT 
 ip.*,pp.*
FROM
 ${ref('ecf2_post_2021_full_teacher_induction_periods')} AS ip
LEFT JOIN participant_profiles as pp
on ip.trn = pp.trn
