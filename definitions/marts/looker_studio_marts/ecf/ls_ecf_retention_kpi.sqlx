config {
    type: "table",
    bigquery: {
        clusterBy: ["lead_provider_name"]
    },
    description: "Objective: To produce tailored output for ECF Contract Management Dashboard to display retention at the points available for each Lead Provider. \n \
  Summary: \n \
    ECF Retention is the number or participants split by participant type(PT) (ECT or Mentor) still with the Lead Provider(LP) at the cut-off & mid-point dates based on the statement cohort of the participants started declaration. \n \
    This output produces a table where for each LP-PT combination the number of started & retained participants for each point in time. \n \
    The points in time are: 2022 cut-off, 2023 mid-point & cut-off and 2024 mid-point & cut-off.",
    columns: {
        lead_provider_name: "The name of the lead provider associated with the started declaration that qualifies the participant to be included in relevant starts.",
        participant_type: "Either ECT or Mentor depending on participant_type from their participant_profile.",
        started_participants_2022_cut_off: "The number of participants split by participant_type for the lead provider where the started declaration in the 2022 statement cohort is funded (e/p/p) as of the cut-off date 31/07/2024.",
        started_participants_2023_mid_point: "The number of participants split by participant_type for the lead provider where the started declaration in the 2023 statement cohort is funded (e/p/p) as of the mid-point date 31/07/2024.",
        started_participants_2023_cut_off: "The number of participants split by participant_type for the lead provider where the started declaration in the 2023 statement cohort is funded (e/p/p) as of the cut-off date 31/07/2025.",
        started_participants_2024_mid_point: "The number of participants split by participant_type for the lead provider where the started declaration in the 2024 statement cohort is funded (e/p/p) as of the mid-point date 31/07/2025.",
        started_participants_2024_cut_off: "The number of participants split by participant_type for the lead provider where the started declaration in the 2024 statement cohort is funded (e/p/p) as of the cut-off date 31/07/2026.",
        retained_participants_2022_cut_off: "The number of started participants split by participant_type for the lead provider where they are still associated with the same lead provider and their status is not withdrawn as of the cut-off date 31/07/2024. Some withdrawn Mentors are still considered retained if the ECTs they were mentoring are associated with an active Mentor as of the same cut-off date.",
        retained_mid_point_participants_2023_mid_point: "The number of started participants split by participant_type for the lead provider where they are still associated with the same lead provider and their status is not withdrawn as of the mid-point date 31/07/2024. Some withdrawn Mentors are still considered retained if the ECTs they were mentoring are associated with an active Mentor as of the same mid-point date.",
        retained_participants_2023_cut_off: "The number of started participants split by participant_type for the lead provider where they are still associated with the same lead provider and their status is not withdrawn as of the cut-off date 31/07/2025. Some withdrawn Mentors are still considered retained if the ECTs they were mentoring are associated with an active Mentor as of the same cut-off date.",
        retained_participants_2024_mid_point: "The number of started participants split by participant_type for the lead provider where they are still associated with the same lead provider and their status is not withdrawn as of the mid-point date 31/07/2025. Some withdrawn Mentors are still considered retained if the ECTs they were mentoring are associated with an active Mentor as of the same mid-point date.",
        retained_participants_2024_cut_off: "The number of started participants split by participant_type for the lead provider where they are still associated with the same lead provider and their status is not withdrawn as of the cut-off date 31/07/2026. Some withdrawn Mentors are still considered retained if the ECTs they were mentoring are associated with an active Mentor as of the same cut-off date."
    }
}

  /*** ECF Retention KPI LS Output Mart ***/

/*
  Objective: To produce tailored output for ECF Contract Management Dashboard to display retention at the points available for each Lead Provider.

  Summary:
    ECF Retention is the number or participants split by participant type(PT) (ECT or Mentor) still with the Lead Provider(LP) at the cut-off & mid-point dates based on the statement cohort of the participants started declaration.
    This output produces a table where for each LP-PT combination the number of started & retained participants for each point in time.
    The points in time are: 2022 cut-off, 2023 mid-point & cut-off and 2024 mid-point & cut-off.
*/

--- Gather unique list of Lead Providers (LP) & Participant Types (PT)
WITH lead_providers AS (
  SELECT
     DISTINCT cpd_lead_provider_name AS lead_provider_name
    ,participant_type
  FROM
    ${ref('ecf_retention_kpi_cut_off_date')}
),

--- Gather 2022 cut-off started & retained counts for each LP-PT combination.
retention_2022_cut_off AS(
  SELECT
     cpd_lead_provider_name AS lead_provider_name
    ,participant_type
    ,COUNT(1) AS started_participants_2022_cut_off
    ,SUM(CASE WHEN participant_retained_at_cut_off = TRUE THEN 1 ELSE 0 END) AS retained_participants_2022_cut_off
  FROM
    ${ref('ecf_retention_kpi_cut_off_date')}
  WHERE
    statement_cohort = 2022
  GROUP BY
     cpd_lead_provider_name
    ,participant_type
),

--- Gather 2023 mid-point started & retained counts for each LP-PT combination.
retention_2023_mid_point AS(
  SELECT
     cpd_lead_provider_name AS lead_provider_name
    ,participant_type
    ,COUNT(1) AS started_participants_2023_mid_point
    ,SUM(CASE WHEN participant_retained_at_mid_point = TRUE THEN 1 ELSE 0 END) AS retained_participants_2023_mid_point
  FROM
    ${ref('ecf_retention_kpi_mid_point_date')}
  WHERE
    statement_cohort = 2023
  GROUP BY
     cpd_lead_provider_name
    ,participant_type
),

--- Gather 2023 cut-off started & retained counts for each LP-PT combination.
retention_2023_cut_off AS(
  SELECT
     cpd_lead_provider_name AS lead_provider_name
    ,participant_type
    ,COUNT(1) AS started_participants_2023_cut_off
    ,SUM(CASE WHEN participant_retained_at_cut_off = TRUE THEN 1 ELSE 0 END) AS retained_participants_2023_cut_off
  FROM
    ${ref('ecf_retention_kpi_cut_off_date')}
  WHERE
    statement_cohort = 2023
  GROUP BY
     cpd_lead_provider_name
    ,participant_type
),

--- Gather 2024 mid-point started & retained counts for each LP-PT combination.
retention_2024_mid_point AS(
  SELECT
     cpd_lead_provider_name AS lead_provider_name
    ,participant_type
    ,COUNT(1) AS started_participants_2024_mid_point
    ,SUM(CASE WHEN participant_retained_at_mid_point = TRUE THEN 1 ELSE 0 END) AS retained_participants_2024_mid_point
  FROM
    ${ref('ecf_retention_kpi_mid_point_date')}
  WHERE
    statement_cohort = 2024
  GROUP BY
     cpd_lead_provider_name
    ,participant_type
),

--- Gather 2024 cut-off started & retained counts for each LP-PT combination.
retention_2024_cut_off AS(
  SELECT
     cpd_lead_provider_name AS lead_provider_name
    ,participant_type
    ,COUNT(1) AS started_participants_2024_cut_off
    ,SUM(CASE WHEN participant_retained_at_cut_off = TRUE THEN 1 ELSE 0 END) AS retained_participants_2024_cut_off
  FROM
    ${ref('ecf_retention_kpi_cut_off_date')}
  WHERE
    statement_cohort = 2024
  GROUP BY
     cpd_lead_provider_name
    ,participant_type
)

--- Combine CTEs into final output where each point in time combination of started & retained appear as separate columns.
SELECT
   lead_provider_name
  ,participant_type
  ,retention_2022_cut_off.started_participants_2022_cut_off
  ,retention_2022_cut_off.retained_participants_2022_cut_off
  ,retention_2023_mid_point.started_participants_2023_mid_point
  ,retention_2023_mid_point.retained_participants_2023_mid_point
  ,retention_2023_cut_off.started_participants_2023_cut_off
  ,retention_2023_cut_off.retained_participants_2023_cut_off
  ,retention_2024_mid_point.started_participants_2024_mid_point
  ,retention_2024_mid_point.retained_participants_2024_mid_point
  ,retention_2024_cut_off.started_participants_2024_cut_off
  ,retention_2024_cut_off.retained_participants_2024_cut_off
FROM
  lead_providers
LEFT JOIN
  retention_2022_cut_off 
USING(
   lead_provider_name
  ,participant_type
)
LEFT JOIN
  retention_2023_mid_point 
USING(
   lead_provider_name
  ,participant_type
)
LEFT JOIN
  retention_2023_cut_off 
USING(
   lead_provider_name
  ,participant_type
)
LEFT JOIN
  retention_2024_mid_point 
USING(
   lead_provider_name
  ,participant_type
)
LEFT JOIN
  retention_2024_cut_off 
USING(
   lead_provider_name
  ,participant_type
)