config {
    type: "table",
    assertions: {
        uniqueKey: ["declaration_id"]
    },
    bigquery: {
        partitionBy: "DATE(declaration_date)",
        clusterBy: ["cpd_lead_provider_name", "participant_profile_id"]
    },
    description: "This is #3 in a series of Assurance 'Bad Shapes' marts that identify declarations received that fall outside the expected declaration rules. This mart identifies start declarations from a 2025 statement cohort or later submitted without evidence.",
    columns: {
        declaration_id: 'ID associated with the declaration which qualifies as falling into a bad shape',
        declaration_type: 'The type of the declaration which indicates how far along a participant is in their training',
        declaration_created_at_date: 'The date the declaration was submitted, this field is only relevant to determine which sequence should be applied, it is not used to compare declarations against each other',
        declaration_date: 'The date the declaration was evidenced, when the participant qualified to be declared against. This field is key for determining where a declaration falls in relation to other declarations because it relates to when in the participants training it was evidenced',
        participant_profile_id: "ID of each participant's profile.",
        state: 'The financial state of the declaration - whether it has been paid yet or not and whether it is eligible for funding - only paid, payable and eligible declarations are relevant for bad shapes checks',
        cpd_lead_provider_name: 'The name of the lead provider that submitted the declaration',
        statement_id: 'The associated financial statement the declaration was included on',
        schedule_identifier: 'The training schedule of the participant - this indicates when they started on the programme and whether they followed a standard pattern of training. Reduced schedules do not have to have a started declaration and their completed declaration can be submitted at any point because they do not have to complete the full length of training',
        participant_type: 'Whether the participant is an ECT or a mentor',
        cohort: 'The cohort on the participants latest induction record. This may not be the cohort they commenced training on. This mart only includes participants from cohorts from 2023 onwards though it is unlikely to see any participants prior to the 2025 cohort because only started declarations from statements in the 2025 cohort are permitted.',
        statement_cohort: 'This is the cohort the declaration was submitted against. This might not match the participants induction cohort because the participant may have been rolled onto a new cohort since this declaration was submitted. Only statement cohorts from 2025 and after are included',
        trn: {
            description: "TRN associated with the ECT's profile.",
            bigqueryPolicyTags: ["projects/ecf-bq/locations/europe-west2/taxonomies/6302091323314055162/policyTags/301313311867345339"]
        },
        evidence_held: 'The evidence type supplied by the lead provider when the declaration was submitted. Its a requirement that started declarations from 2025 onwards have an evidence type provided.'
    }
}

SELECT
  declarations.declaration_id,
  declarations.declaration_type,
  declarations.declaration_created_at_date,
  declarations.declaration_date,
  declarations.participant_profile_id,
  declarations.state,
  cpd_lead_provider_name,
  statement_id,
  schedule_identifier,
  participant_type,
  cohort,
  statement_cohort,
  trn,
  evidence_held
FROM
  ${ref("ecf_declarations")} declarations
INNER JOIN
  ${ref("participant_declarations_latest_ecf1")} part_decs
ON
  declarations.declaration_id = part_decs.id
WHERE
  declarations.declaration_type = 'started'
  AND declarations.state IN ( 'eligible',
    'payable',
    'paid')
  AND evidence_held IS NULL
  AND statement_cohort =2025 and cohort >= 2023
