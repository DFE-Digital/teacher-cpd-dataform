config {
    type: "table",
    assertions: {
        uniqueKey: ["id"]
    },
    bigquery: {
        partitionBy: "DATE(created_at)",
        clusterBy: ["training_status", "reason", "cpd_lp_name"]
    },
    description: "This mart provides a report that gives the current training_status for all ECF participants. This information is provided to users in the ECF Self-Serve Dashboard where it is filtered to show only withdrawn or deferred participants and the reason for their withdrawal/deferall. The primary users are the ECF schools team.",
    columns: {
        id: "Unique ID of the record being pulled from the participant_profile_states table.",
        created_at: "Timestamp of when the record being pulled from participant_profile_states was created. Since participants can go through different training statuses, only the most recently created record for each participant is pulled.",
        updated_at: "Timestamp of when the record being pulled from participant_profile_states was updated.",
        lead_provider_id: "ID of the Lead Provider linked to the participant in participant_profile_state.",
        profile_id: "participant_profile_id of each participant's profile.",
        reason: "Explains why a participant is in a certain state.",
        training_status: "This is the participant's training_status. It can be active, deferred, or withdrawn.",
        last_streamed_event_occurred_at: "Timestamp of when the record  from participant_profile_states was most recently imported or updated",
        last_streamed_event_type: "Whether the most recent event for this record from participant_profile_states was it being imported or updated.",
        cohort: "Participant's cohort from ecf_indcutions_dedupe.",
        schedule_identifier: "Participant's schedule from ecf_indcutions_dedupe.",
        cpd_lp_name: "Name of the Lead Provider linked to the participant.",
        participant_type: "Details if a participant is an ECT or Mentor."
    }
}
WITH most_recent_training_status as (
SELECT
  ppsl.id,
  ppsl.created_at,
  ppsl.updated_at,
  ppsl.cpd_lead_provider_id AS lead_provider_id,
  ppsl.participant_profile_id AS profile_id,
  ppsl.reason,
  ppsl.state as training_status,
  ppsl.last_streamed_event_occurred_at,
  ppsl.last_streamed_event_type,
  ecfid.cohort AS cohort,
  ecfid.schedule_identifier AS schedule_identifier,
  ecfid.lead_provider_name AS cpd_lp_name,
  CASE
    WHEN ecfid.participant_type LIKE '%ECT' THEN 'ECT'
    WHEN ecfid.participant_type LIKE '%Mentor' THEN 'Mentor'
END
  AS participant_type,

FROM
  ${ref(`participant_profile_states_latest_ecf1`)} ppsl
LEFT JOIN
  ${ref(`lead_providers_latest_ecf1`)} lpl
USING
  (cpd_lead_provider_id)
INNER JOIN
  ${ref(`ecf_inductions_dedupe`)} ecfid
USING
  (participant_profile_id)
QUALIFY
  ROW_NUMBER() OVER (PARTITION BY ppsl.participant_profile_id ORDER BY ppsl.created_at DESC) = 1)

  SELECT *
  FROM most_recent_training_status
WHERE training_status != 'active'
