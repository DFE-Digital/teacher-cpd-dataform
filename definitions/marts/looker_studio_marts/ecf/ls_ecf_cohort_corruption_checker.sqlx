config {
    type: "table",
    assertions: {
        uniqueKey: ["id"]
    },
    bigquery: {
        partitionBy: "DATE(declaration_date)",
        clusterBy: ["cohort", "statement_cohort", "suggested_cohort_from_declaration", "check_source"]
    },
    description: "This mart creates a list of all declarations for a participant where one of the participant's declarations doesn't align with the participant's cohort.",
    columns: {
        id: "ID of declaration",
        created_at: "Timestamp when the declaration was created.",
        updated_at: "Timestamp of when any updates occured to the declaration.",
        course_identifier: "Identifier for ECF or NPQ course declaration is associated with.",
        cpd_lead_provider_id: "ID of Lead Provider that has made the declaration.",
        declaration_date: "Date evidence was received.",
        declaration_type: "The name of the stage the declaration has been made against.",
        delivery_partner_id: "ID of the ECF Delivery Partner associated with the Lead Provider-participant combo.",
        evidence_held: "Details any evidence provided with the declaration.",
        mentor_user_id: "User ID of the mentor associated with the declared-against ECT",
        participant_profile_id: "ID of the participant's profile.",
        pupil_premium: "Boolean for pupil premium eligibility.",
        sparsity_uplift: "Boolean for sparsity uplift eligibility.",
        state: "State of the declaration. Can be submitted, eligible, payable, paid, ineligible, voided, awaiting_clawback, and clawed_back.",
        superseded_by_id: "NEED PROPER DEFINITION",
        type: "Says if it belongs to either the ECF or NPQ programme.",
        user_id: "Used ID of the participant being declared against.",
        last_streamed_event_occurred_at: "Timestamp of when this record was most recently imported or updated.",
        last_streamed_event_type: "Whether the most recent event was this entity being imported or updated.",
        cohort: "Participant's cohort from either npq_enrolments or ecf_indcutions_dedupe.",
        schedule_identifier: "Participant's schedule from either npq_enrolments or ecf_indcutions_dedupe.",
        statement_cohort: "Cohort from the financial statement a declaration is stored against.",
        cpd_lp_name: "Name of Lead Provider for NPQ or ECF course",
        cpd_dp_name: "Name of Delivery Partner for ECF course",
        programme: "Shortening of 'type'. Says if it belongs to either the ECF or NPQ programme.",
        suggested_cohort_from_declaration: "Calculates where a declaration date falls in the declaration milestone window across an entire cohor and therefore the suggested cohort.",
        check_source: "Details which (if any) of the checks a declaration belongs to."
    }
}

WITH
  full_declaration_milestone_table AS (
    /*This section creates a list of all submitted/eligible/payable/paid declarations for a participant, pulls in details from their current induction record, and then joins on the two static tables ecf_schedule_milestone_mapping and ecf_schedule_milestone_mapping_condensed to later make comparisons against the declaration_date of each participant*/
    SELECT
      dec_cpd.id AS declaration_id,
      dec_cpd.user_id,
      dec_cpd.participant_profile_id,
      ecf_id.induction_record_id,
      ecf_id.cohort,
      ecf_id.schedule_identifier,
      ecf_id.school_urn,
      CASE
        WHEN participant_type LIKE 'ParticipantProfile::ECT' THEN 'ECT'
        WHEN participant_type LIKE 'ParticipantProfile::Mentor' THEN 'Mentor'
      ELSE
      NULL
    END
      AS participant_type,
      dec_cpd.course_identifier,
      dec_cpd.declaration_type,
      DATE(dec_cpd.declaration_date) AS dec_date_short,
      dec_cpd.state,
      dec_cpd.cpd_lp_name,
      dec_cpd.cpd_dp_name,
      schedule_milestones.started_dec_milestone_date,
      schedule_milestones.started_dec_start_date,
      schedule_milestones.ret1_dec_milestone_date,
      schedule_milestones.ret1_dec_start_date,
      schedule_milestones.ret2_dec_milestone_date,
      schedule_milestones.ret2_dec_start_date,
      schedule_milestones.ret3_dec_milestone_date,
      schedule_milestones.ret3_dec_start_date,
      schedule_milestones.ret4_dec_milestone_date,
      schedule_milestones.ret4_dec_start_date,
      schedule_milestones.completed_dec_milestone_date,
      schedule_milestones.completed_dec_start_date,
      sched_mile_cond.start_date_2021,
      sched_mile_cond.milestone_date_2021,
      sched_mile_cond.start_date_2022,
      sched_mile_cond.milestone_date_2022,
      sched_mile_cond.start_date_2023,
      sched_mile_cond.milestone_date_2023,
    FROM
      ${ref(`ls_declarations_provider_names`)} AS dec_cpd
    LEFT JOIN
      ${ref(`ecf_inductions_dedupe`)} AS ecf_id
    ON
      dec_cpd.participant_profile_id=ecf_id.participant_profile_id
    LEFT JOIN
      `static_tables.ecf_schedule_milestone_mapping` AS schedule_milestones
    ON
      ecf_id.schedule_identifier=schedule_milestones.schedule_identifier
      AND ecf_id.cohort=schedule_milestones.cohort
    LEFT JOIN
      `static_tables.ecf_schedule_milestone_mapping_condensed` AS sched_mile_cond
    ON
      dec_cpd.declaration_type=sched_mile_cond.declaration_type
    WHERE
      dec_cpd.course_identifier LIKE 'ecf%'
      AND dec_cpd.state IN ('submitted',
        'eligible',
        'payable',
        'paid')),
  statement_information AS (
    /*This section pulls together statement info for all declarations from the tables statement_line_items and statements and gives the cohort associated with the statement that each declaration was made against*/
    SELECT
      stat_li.participant_declaration_id,
      stat_li.state AS statement_state,
      stat.name AS statement_name,
      stat.type AS statement_type,
      cpd_lp.name AS statement_cpd_lp_name,
      cohort.start_year AS statement_cohort
    FROM
      ${ref(`statement_line_items_latest_cpd`)} stat_li
    LEFT JOIN
      ${ref(`statements_latest_cpd`)} stat
    ON
      stat_li.statement_id=stat.id
    LEFT JOIN
      ${ref(`cpd_lead_providers_latest_cpd`)} cpd_lp
    ON
      stat.cpd_lead_provider_id=cpd_lp.id
    LEFT JOIN
      ${ref(`cohorts_latest_cpd`)} cohort
    ON
      stat.cohort_id=cohort.id
    WHERE
      stat.type LIKE '%ECF%'),
  statement_cohort_checks AS (
    SELECT
      fdmt.*,
      /*This CASE statement finds how far away a declaration is from its relevant declaration milestone window, as defined in the table ecf_schedule_milestone_mapping. Since a declaration can be on either side of the declaration window, the CASE statement calculates the greatest difference.*/
      CASE
        WHEN declaration_type = 'started' THEN GREATEST(ABS(DATE_DIFF(started_dec_start_date,dec_date_short,DAY)),ABS(DATE_DIFF(started_dec_milestone_date,dec_date_short,DAY)))
        WHEN declaration_type = 'retained-1' THEN GREATEST(ABS(DATE_DIFF(ret1_dec_start_date,dec_date_short,DAY)),ABS(DATE_DIFF(ret1_dec_milestone_date,dec_date_short,DAY)))
        WHEN declaration_type = 'retained-2' THEN GREATEST(ABS(DATE_DIFF(ret2_dec_start_date,dec_date_short,DAY)),ABS(DATE_DIFF(ret2_dec_milestone_date,dec_date_short,DAY)))
        WHEN declaration_type = 'retained-3' THEN GREATEST(ABS(DATE_DIFF(ret3_dec_start_date,dec_date_short,DAY)),ABS(DATE_DIFF(ret3_dec_milestone_date,dec_date_short,DAY)))
        WHEN declaration_type = 'retained-4' THEN GREATEST(ABS(DATE_DIFF(ret4_dec_start_date,dec_date_short,DAY)),ABS(DATE_DIFF(ret4_dec_milestone_date,dec_date_short,DAY)))
        WHEN declaration_type = 'completed' THEN GREATEST(ABS(DATE_DIFF(completed_dec_start_date,dec_date_short,DAY)),ABS(DATE_DIFF(completed_dec_milestone_date,dec_date_short,DAY)))
    END
      AS declaration_out_by_x_days,
      si.statement_cpd_lp_name,
      si.statement_cohort,
      /*This CASE statement works out if a declaration date falls in the declaration milestone window across an entire cohort, as defined in the table ecf_schedule_milestone_mapping_condensed. There is crossover between milestone_date_2022 and start_date_2023 for started declarations.*/
      CASE
        WHEN dec_date_short BETWEEN start_date_2021 AND milestone_date_2021 THEN 2021
        WHEN dec_date_short BETWEEN start_date_2022
      AND milestone_date_2022 THEN 2022
        WHEN dec_date_short BETWEEN start_date_2023 AND milestone_date_2023 THEN 2023
      ELSE
      NULL
    END
      AS suggested_cohort_from_declaration
    FROM
      full_declaration_milestone_table fdmt
    LEFT JOIN
      statement_information si
    ON
      fdmt.declaration_id=si.participant_declaration_id
    WHERE /*This excludes declarations we already know do not align with the statement cohort but the financial statements have been closed so there is no action to be taken.*/
      declaration_id NOT IN ('59e5d04c-0741-4e1d-b1c3-8467cafc3ead',
      'bcf10833-cbce-470b-8afb-6fdea087347b',
      '0c643297-378e-4fc7-9a82-4bf882369bab',
      '45d8c5c4-d76e-494a-9570-3c1fa2e26b08',
      '069fe4b1-3e3f-4b73-a095-20454717f3e1',
      '3115cd3a-08b1-4afd-a50b-9b595efef6e8',
      '58f73d2a-c5eb-4034-b131-5e9394705bbe',
      'dde9d124-cb67-4037-a84a-ef8bc2d4e5a9',
      '3d74e41b-4647-4f93-b4a1-cbdae4ea03ca',
      '98f30bf7-c3e6-4794-b3f3-1469e441b6a2',
      'dff40840-0c86-48d4-bbb2-75cf9c1393dd',
      '5e9497a8-fbcd-41c9-9c91-902d6f8d1a13',
      '21e63f72-93ab-449e-ae16-2645150a0c55',
      '3136548a-cd8b-4240-8577-71ca130897a7',
      '6b5e82a7-58a5-4904-9375-26eec27ad16f',
      '8d27404d-e82b-45cc-97e5-77bb83b4be8f',
      '68c45337-61f1-4f77-aad5-dd8baca851f6',
      '76ba686a-64b6-45f1-aacc-8f247897b4b7',
      '041c32ae-04ec-4bac-aad2-5baf204c9703',
      'b9a9b61f-c9da-407e-a8e1-a190471f7928',
      '0fe70258-8c92-4fa8-8abd-9bfa47bde443',
      '7c216ac7-6ff5-4b1b-accb-9d87721f937a',
      '98b051f7-6b96-4731-9419-830c8bf98efb',
      '350a8988-e306-490f-824b-87ce2351a789',
      '2fdd1ea2-d706-421d-b30c-27a1192a0ea9',
      'db492412-53a6-429c-9480-1629e54aed2f',
      '0301f4eb-a19b-474b-a1da-e5cc0cfa8352',
      'e5d83740-9b26-4817-b2ac-257a9f3c3a26',
      '3695bced-a280-4e3c-b15f-1430c271e7a1',
      'bbcb45eb-48d2-40d5-a637-fc7b5b27ede3',
      'c651aeed-3f6b-4aed-b915-d8324a757af2',
      'a2348288-bfd2-42fb-ba2a-fdfbd6067ee1',
      'd7ca9990-cb63-4f47-8ae6-606218ffbca5',
      'df0fa656-0446-4bf1-ba05-90052fa4d146',
      'b79474b6-7635-46f1-b9e3-5a7f47582930',
      '98206908-b175-43af-9faa-f025d46e612c',
      '02fdeecf-b9e6-4de8-914c-7fecd0e47557',
      '6fc17638-1838-403d-9abb-a95ee6732d5d',
      '014d61aa-c0b0-486e-ad30-097f241ca0cc',
      '64f403a4-4c94-4763-8f36-46906ef37000',
      'bf545cb3-a513-4bd4-8038-fad655959010',
      '97c28880-6d00-4331-a6e4-622667e1521a',
      '7ad36b2b-b926-467b-8902-fcee2dd8046d',
      'bfdcb633-5d62-49ae-8d64-457587d9d279',
      'c47b1e46-0162-4932-949e-41bb149f2e1b',
      'b6dc508e-790f-4df3-a92c-c7705cfcc236',
      '1dbedd79-15b8-4833-9667-26a8d38ae50b')),
  full_cohort_checker_output AS (
    SELECT
      *,
      CASE /*This CASE statement works out which (if any) of the checks a declaration belongs to, either the financial statement cohort check or comparing declaration date check. Declarations can belong to one or both of the checks, and we're also pulling together all of a participant's declarations, hence the need for the NULL ending.*/
        WHEN statement_cohort <> cohort AND (suggested_cohort_from_declaration <> cohort 
      AND ((schedule_identifier LIKE 'ecf-standard%'
          AND ((declaration_type = 'started'
              AND (dec_date_short NOT BETWEEN started_dec_start_date
                AND started_dec_milestone_date))
            OR (declaration_type = 'retained-1'
              AND (dec_date_short NOT BETWEEN ret1_dec_start_date
                AND ret1_dec_milestone_date))
            OR (declaration_type = 'retained-2'
              AND (dec_date_short NOT BETWEEN ret2_dec_start_date
                AND ret2_dec_milestone_date))
            OR (declaration_type = 'retained-3'
              AND (dec_date_short NOT BETWEEN ret3_dec_start_date
                AND ret3_dec_milestone_date))
            OR (declaration_type = 'retained-4'
              AND (dec_date_short NOT BETWEEN ret4_dec_start_date
                AND ret4_dec_milestone_date))
            OR (declaration_type = 'completed'
              AND (dec_date_short NOT BETWEEN completed_dec_start_date
                AND completed_dec_milestone_date))))
        OR (schedule_identifier LIKE ANY ('ecf-extended%',
            'ecf-reduced%',
            'ecf-replacement%')
          AND ((declaration_type = 'started'
              AND dec_date_short < started_dec_start_date)
            OR (declaration_type = 'retained-1'
              AND dec_date_short < ret1_dec_start_date)
            OR (declaration_type = 'retained-2'
              AND dec_date_short < ret2_dec_start_date)
            OR (declaration_type = 'retained-3'
              AND dec_date_short < ret3_dec_start_date)
            OR (declaration_type = 'retained-4'
              AND dec_date_short < ret4_dec_start_date)
            OR (declaration_type = 'completed'
              AND dec_date_short < completed_dec_start_date))))) THEN "in both checks"
        WHEN suggested_cohort_from_declaration <> cohort
      AND ((schedule_identifier LIKE 'ecf-standard%'
          AND ((declaration_type = 'started'
              AND (dec_date_short NOT BETWEEN started_dec_start_date
                AND started_dec_milestone_date))
            OR (declaration_type = 'retained-1'
              AND (dec_date_short NOT BETWEEN ret1_dec_start_date
                AND ret1_dec_milestone_date))
            OR (declaration_type = 'retained-2'
              AND (dec_date_short NOT BETWEEN ret2_dec_start_date
                AND ret2_dec_milestone_date))
            OR (declaration_type = 'retained-3'
              AND (dec_date_short NOT BETWEEN ret3_dec_start_date
                AND ret3_dec_milestone_date))
            OR (declaration_type = 'retained-4'
              AND (dec_date_short NOT BETWEEN ret4_dec_start_date
                AND ret4_dec_milestone_date))
            OR (declaration_type = 'completed'
              AND (dec_date_short NOT BETWEEN completed_dec_start_date
                AND completed_dec_milestone_date))))
        OR (schedule_identifier LIKE ANY ('ecf-extended%',
            'ecf-reduced%',
            'ecf-replacement%')
          AND ((declaration_type = 'started'
              AND dec_date_short < started_dec_start_date)
            OR (declaration_type = 'retained-1'
              AND dec_date_short < ret1_dec_start_date)
            OR (declaration_type = 'retained-2'
              AND dec_date_short < ret2_dec_start_date)
            OR (declaration_type = 'retained-3'
              AND dec_date_short < ret3_dec_start_date)
            OR (declaration_type = 'retained-4'
              AND dec_date_short < ret4_dec_start_date)
            OR (declaration_type = 'completed'
              AND dec_date_short < completed_dec_start_date)))) THEN "declaration date check"
        WHEN statement_cohort <> cohort THEN "statement check"
      ELSE
      NULL
    END
      AS check_source
    FROM
      statement_cohort_checks
    WHERE
      statement_cohort <> cohort
      OR (suggested_cohort_from_declaration <> cohort
        AND ((schedule_identifier LIKE 'ecf-standard%'
            AND ((declaration_type = 'started'
                AND (dec_date_short NOT BETWEEN started_dec_start_date
                  AND started_dec_milestone_date))
              OR (declaration_type = 'retained-1'
                AND (dec_date_short NOT BETWEEN ret1_dec_start_date
                  AND ret1_dec_milestone_date))
              OR (declaration_type = 'retained-2'
                AND (dec_date_short NOT BETWEEN ret2_dec_start_date
                  AND ret2_dec_milestone_date))
              OR (declaration_type = 'retained-3'
                AND (dec_date_short NOT BETWEEN ret3_dec_start_date
                  AND ret3_dec_milestone_date))
              OR (declaration_type = 'retained-4'
                AND (dec_date_short NOT BETWEEN ret4_dec_start_date
                  AND ret4_dec_milestone_date))
              OR (declaration_type = 'completed'
                AND (dec_date_short NOT BETWEEN completed_dec_start_date
                  AND completed_dec_milestone_date))))
          OR (schedule_identifier LIKE ANY ('ecf-extended%',
              'ecf-reduced%',
              'ecf-replacement%')
            AND ((declaration_type = 'started'
                AND dec_date_short < started_dec_start_date)
              OR (declaration_type = 'retained-1'
                AND dec_date_short < ret1_dec_start_date)
              OR (declaration_type = 'retained-2'
                AND dec_date_short < ret2_dec_start_date)
              OR (declaration_type = 'retained-3'
                AND dec_date_short < ret3_dec_start_date)
              OR (declaration_type = 'retained-4'
                AND dec_date_short < ret4_dec_start_date)
              OR (declaration_type = 'completed'
                AND dec_date_short < completed_dec_start_date))))))
SELECT
  DISTINCT a.*,
  c.suggested_cohort_from_declaration,
  c.check_source
FROM
  `dataform.ls_declarations_provider_names` a
INNER JOIN
  full_cohort_checker_output b
ON
  a.participant_profile_id = b.participant_profile_id
LEFT JOIN
  full_cohort_checker_output c
ON
  a.id=c.declaration_id
WHERE
  a.programme = 'ECF'
