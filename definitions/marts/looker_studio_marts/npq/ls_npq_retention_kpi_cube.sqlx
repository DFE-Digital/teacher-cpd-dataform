config {
    type: "table",
    bigquery: {
        partitionBy: "",
        clusterBy: ["provider_name", "cohort_number", "short_course_name"]
    },
    description: "This mart calculates the number of participants enrolled in an NPQ course with a funded start declaration, grouped by Lead Provider. It is designed for use in the NPQ Retention KPI dashboard within the NPQ Contract Management Suite in Looker Studio. The retention KPI supports performance discussions between Lead Providers and Contract Managers by breaking down retention rates by NPQ course, Policy Cohort, and Lead Provider. It leverages a cube architecture to facilitate the calculation of these KPIs. Participants are considered retained if they have either completed an NPQ course (with a completed_declaration_state of 'eligible', 'payable', or 'paid') or have started but not yet completed their course (with a training_status of 'active').",
    columns: {
        provider_name: "Name of the provider.",
        cohort_number: "A policy orientated cohort identifier to indicate which cohort a DfE funded applicants lies, this field is calculated through a combination of cohort years and in the years 2021-2023 which 'schedule_identifier' they will be a part of.",
        short_course_name: "Shortened name of the course.",
        completed_count: "The number of applicates finded by the DfE which have completed their NPQ course. This counts all completed NPQ courses which are defined as where the retention_status is 'eligible', 'payable' or 'paid'.",
        active_count: "Number of applicants funded by the DfE who are currently undertaking an NPQ course. This counts applicants with an 'active' course status.",
        deferred_count: "Number of applicants funded by the DfE who have deferred their NPQ course. This counts applicants with a 'deferred' course status.",
        withdrawn_count: "Number of applicants funded by the DfE who a withdrawn from their NPQ course. This counts applicants with a 'withdrawn' course status.",
        total_application_count: "The total number of DfE funded applications recieved",
        retention_numerator: "The calculated numerator of the retention KPI. Retention is defined as the retention rate for all completed and active counts, deferred statuses are excluded from this metric. retention_rate = (completed_count+active_count)/(completed_count+active_count+withdrawn).",
        retention_denominator: "The calculated denominator of the rentention KPI. Retention is defined as the retention rate for all completed and active counts, deferred statuses are excluded from this metric. retention_rate = (completed_count+active_count)/(completed_count+active_count+withdrawn)."
    }
}

WITH
  assign_cohort_number AS (
  SELECT
    application_id,
    provider_name,
    short_course_name,
    course_identifier,
    course_name,
    funded_start_declaration,
    cohort,
    schedule_identifier,
    cohort_number,
    COALESCE((CASE
          WHEN completed_declaration_state IN ('eligible', 'payable', 'paid') THEN 'completed'
          ELSE NULL
      END
        ),training_status) AS retention_status
  FROM
    ${ref('npq_enrolments')}
  WHERE
    funded_start_declaration)
SELECT
  provider_name,
  cohort_number,
  short_course_name,
  COUNT(*) AS total_application_count,
  COUNTIF(retention_status = 'completed') AS completed_count,
  COUNTIF(retention_status = 'active')    AS active_count,
  COUNTIF(retention_status = 'deferred')  AS deferred_count,
  COUNTIF(retention_status = 'withdrawn') AS withdrawn_count,
  COUNTIF(retention_status IN ('completed', 'active')) AS retention_numerator,
  COUNTIF(retention_status IN ('completed', 'active', 'withdrawn')) AS retention_denominator
FROM
  assign_cohort_number
GROUP BY
  provider_name,
  cohort_number,
  short_course_name
