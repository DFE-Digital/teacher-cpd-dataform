config {
    type: "table",
    assertions: {
        uniqueKey: ["application_id"]
    },
    description: "This mart is made to help out NPQ Funding Team with payments for the targeted support fund. It returns all NPQ applications that meet 1 or more of the eligibility criteria but are not currently included in the list for TSF funding.",
    columns: {
        ecf_user_id: "This comes from the teacher profile associated with the participant profile. User ID also exists independently in other tables.",
        application_trn: "This is the TRN provided at the point of application",
        verified_trn: "This is the TRN from the Teacher Profile associated with the ecf_user_id",
        application_id: "ID of each unique application. Declarations are joined to the application using this id and it is also the participant_profile_id in the declarations table",
        course_identifier: "Shorthand of NPQ course. This comes from the participant's latest declaration.",
        cohort: "The cohort/academic year corresponding to when the participant started their course.",
        school_urn: "The unique reference number of the participant's school.",
        school_name: "Name of participant's school.",
        la_name: "Name of local authority.",
        la_code: "Code for the local authority",
        primary_establishment: "Records whether the user's establishment at time of registration was a primary establishment or not. Based on GIAS data.",
        establishment_type: "Establishment type as stored in GIAS on 10/05/2024.",
        phase_of_education: "Establishment's phase of education as stored in GIAS on 10/05/2024.",
        establishment_status: "Establishment's status (Open etc.) as stored in GIAS on 10/05/2024",
        number_of_pupils: "Number of pupils at the establishment as recorded on GIAS at time of registration.",
        gias_number_of_pupils: "Number of pupils at the establishment as recorded on GIAS on 10/05/2024.",
        date_accessed: "The date at which the GIAS data was extracted and saved in the static table.",
        targeted_delivery_funding_eligibility: "TRUE/FALSE for whether a school is eligible for TDF. Also acts as a flag for TSF Primary.",
        tsf_primary_eligibility: "TRUE/FALSE for whether a school is eligible for TSF (£200) owing to it being a primary establishment.",
        tsf_primary_plus_eligibility: "TRUE/ FALSE for whether a school is eligible for the base £200 as well as the additional £600 based on whether they are a primary school and have fewer than 150 pupils. Pupil count based on GIAS data at time of registration.",
        pupils_at_reg_chk: "1 or 0 if the application has 1-600 pupils recorded at time of registration.",
        gias_pupils_chk: "1 or 0 if GIAS has 1-600 pupils recorded as of 10/05/2024.",
        gias_phase_chk: "1 or 0 if the GIAS Phase of Education on 10/05/2024 is either 'Primary' or 'Middle Deemed Primary'.",
        spec_est_chk: "1 or 0 if the school is on the list of Special 16-19 Establishments."
    }
}
/*** NPQ TSF Possible Inclusion Cases Looker Studio Data Mart ***/
/*
  Objective:
    To produce list of applications possibly eligible for Grant Funding.

  Dependencies:
    static_tables.gias_snapshot_2024_05_10
    static_tables.npq_tsf_additional_applications_to_include
    static_tables.npq_tsf_applications_to_exclude
    static_tables.special_16_19_establishments


  Version:
    1.0 2024-05-17 Tony Page
      Initial Build

    1.1 2024-05-21 Tony Page
      Added criteria to only show applications that meet 1 or more of the accepted criteria:
        number of pupils at time of registration between 1 & 600
        GIAS number of pupils as of 2024-05-10 is between 1 & 600
        GIAS phase of education as of 2024-05-10 is 'Primary' or 'Middle Deemed Primary'
        School URN is associated with a Special 16-19 Establishment

*/

-- Get NPQ applications where targeted_delivery_funding_eligibility = FALSE
WITH npq_applications AS(
  SELECT
    ecf_user_id,
    application_trn,
    trn_verified,
    cohort,
    application_ecf_id AS application_id,
    course_identifier,
    school_urn,
    school_name,
    number_of_pupils,
    primary_establishment,
    targeted_delivery_funding_eligibility,
    tsf_primary_eligibility, -- Unused by the service as targeted_delivery_funding_eligibility also acts as the flag for this indicator. Included purely for visibility.
    tsf_primary_plus_eligibility
  FROM
    `dataform.npq_enrolments`
  WHERE
    targeted_delivery_funding_eligibility IS FALSE
    AND 
    started_declaration_state IN (
      'eligible',
      'payable',
      'paid')
    AND 
    started_declaration_date >= '2023-10-01'
    AND
    course_identifier NOT IN ('npq-early-headship-coaching-offer', 'npq-additional-support-offer') --EHCO/ASO courses are excluded as per instruction on 16/05/2024.
    AND
    school_urn IS NOT NULL
),

-- Get snapshot of GIAS data
gias_information AS (
  SELECT
     gias.*
    ,code.la_code
  FROM
    `static_tables.gias_snapshot_2024_05_10` gias --Snapshot used as GIAS information needs to remain unchanged in relation to applications contained within output of this query.
  LEFT JOIN
    `static_tables.la_codes_gias_ofsted` code 
  ON
    code.gias_la_name = gias.la_name
  WHERE 
    data_source LIKE 'GIAS%'
),

--- Get additional applications that need to be included.
additional_applications AS (
  SELECT *
  FROM `static_tables.npq_tsf_additional_applications_to_include`
),

--- Get applications in need of exclusion.
excluded_applications AS (
  SELECT *
  FROM `static_tables.npq_tsf_applications_to_exclude`
),

-- Add criteria columns to combined data for review
application_checks AS (
  SELECT
    npq.ecf_user_id,
    npq.application_trn,
    npq.trn_verified AS verified_trn,
    npq.application_id,
    npq.course_identifier,
    npq.cohort,
    npq.school_urn,
    npq.school_name,
    gias.la_name,
    gias.la_code,
    npq.primary_establishment,
    gias.establishment_type AS establishment_type,
    gias.phase_of_education,
    gias.establishment_status,
    npq.number_of_pupils,
    gias.number_of_pupils AS gias_number_of_pupils,
    gias.date_accessed,
    npq.targeted_delivery_funding_eligibility,
    npq.tsf_primary_eligibility,
    npq.tsf_primary_plus_eligibility,
    -- Pupils at time of registration check
    CASE
      WHEN npq.number_of_pupils BETWEEN 1 AND 600 THEN 1 ELSE 0 
    END AS pupils_at_reg_chk,
    -- Pupils in GIAS as of 2024-05-10 check
    CASE
      WHEN gias.number_of_pupils BETWEEN 1 AND 600 THEN 1 ELSE 0 
    END AS gias_pupils_chk,
    -- GIAS as of 2024-05-10 Phase of Education check; Case sensitivity needs to be considered.
    CASE
      WHEN gias.phase_of_education IN ('Primary', 'Middle Deemed Primary') THEN 1 ELSE 0 
    END AS gias_phase_chk,
    -- URN exists in list of Special 16-19 Establishments
    CASE
      WHEN spec.ukprn IS NOT NULL THEN 1 ELSE 0 
    END AS spec_est_chk
  FROM
    npq_applications npq
  LEFT JOIN
    gias_information gias
  ON
    npq.school_urn = gias.establishment_urn
  LEFT JOIN
    excluded_applications ex
  ON
    ex.application_id = npq.application_id
  LEFT JOIN
    additional_applications add 
  ON
    add.application_id = npq.application_id
  LEFT JOIN
    `static_tables.special_16_19_establishments` spec 
  ON
    spec.urn = npq.school_urn
  WHERE --Application should not appear in either included or exlcuded list in order to be reviewed by Grant Funding Team.
    ex.application_id IS NULL
    AND
    add.application_id IS NULL
)

  --- Build output of applications that meet inclusion criteria.
SELECT 
  *
FROM
  application_checks
WHERE
  pupils_at_reg_chk = 1
  OR
  gias_pupils_chk = 1
  OR
  gias_phase_chk = 1
  OR
  spec_est_chk = 1