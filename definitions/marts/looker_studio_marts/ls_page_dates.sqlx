config {
    type: "incremental",
    assertions: {
        uniqueKey: ["application", "page_path", "date"]
    },
    bigquery: {
        partitionBy: "date",
        clusterBy: ["application"]
    },
    description: "Cube containing aggregable metrics about a particular page_path for an application on a date. Does not include data for the current date because this data is incomplete.",
    columns: {
        application: "Name of the application that received requests to view page_path for which metrics have been calculated.",
        page_path: "Path of a page on a service, excluding the domain and query string.",
        date: "Date for which metrics have been calculated",
        number_of_views: "Total number of non-unique views of page_path which occurred on date.",
        number_of_consecutively_unique_views: "Total number of views of page_path which occurred on date, counting consecutive views of the same grouped page_path only once.",
        number_of_exits: "Total number of times a view of page_path on date was the last pageview within a session in session_details.",
        number_of_bounces: "Total number of times a view of page_path on date was both the first and the last pageview within a session in session_details.",
        number_of_unique_views: "Total number of unique authenticated users who viewed page_path on date.",
        number_of_authenticated_views: "Total number of non-unique views of page_path which occurred on date which were part of sessions during which the user was logged in (i.e. had a user_id) for one or more pageview. Useful when combined with number_of_unique_views to calculate view per viewer rate reliably.",
        total_time_on_page: "Total number of seconds spent 'on' page_path on date, where time 'on' page is defined as the length of time between viewing the page and viewing the next page within the same session (if a next pageview happened within the same session)"
    }
}

pre_operations {
  DECLARE date_checkpoint DEFAULT (
    ${when(incremental(),
    `SELECT MAX(date) FROM ${self()}`,
    `SELECT DATE("2000-01-01")`)}
  )
}

SELECT
  application,
  page_path,
  DATE(page_entry_time) AS date,
  SUM(number_of_immediate_repeat_pageviews) AS number_of_views,
  COUNT(*) AS number_of_consecutively_unique_views,
  COUNTIF(next_step = "End of session") AS number_of_exits,
  COUNTIF(next_step = "End of session" AND pageview_number_in_session = 1) AS number_of_bounces,
  COUNT(DISTINCT user_id) AS number_of_unique_views,
  COUNTIF(user_id IS NOT NULL) AS number_of_authenticated_views,
  SUM(time_on_page) AS total_time_on_page
FROM
  ${ref("ls_pageview_next_steps")}
WHERE
  DATE(page_entry_time) < CURRENT_DATE
  AND DATE(page_entry_time) > date_checkpoint
GROUP BY
  application,
  page_path,
  DATE(page_entry_time)